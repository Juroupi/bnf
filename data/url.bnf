<url> ::= <httpaddress> | <ftpaddress>

<httpaddress> ::= "http://" <hostport> <httpaddresspath> <httpaddresssearch>
<httpaddresspath> ::= "/" <path> | ""
<httpaddresssearch> ::= "?" <search> | ""

<ftpaddress> ::= "ftp://" <login> "/" <path> <ftpaddresstype>
<ftpaddresstype> ::= ";" <ftptype> | ""

<login> ::= <loginuser> <hostport>
<loginuser> ::= <user> <loginpass> "@" | ""
<loginpass> ::= ":" <password> | ""

<hostport> ::= <host> | <host> ":" <port>

<host> ::= <hostname> | <hostnumber>

<ftptype> ::= "A" <formcode> | "E" <formcode> | "I" | "L" <digits>

<formcode> ::= "N" | "T" | "C"

<hostname> ::= <ialpha> | <ialpha> "." <hostname>

<hostnumber> ::= <digits> "." <digits> "." <digits> "." <digits>

<port> ::= <digits>

<path> ::= <void> | <segment> | <segment> "/" <path>

<segment> ::= <xpalphas>

<search> ::= <xalphas> | <xalphas> "+" <search>

<user> ::= <alphanum2> | <alphanum2> <user>

<password> ::= <alphanum2> | <alphanum2> <password>

<alphanum2> ::= <alpha> | <digit> | "-" | "_" | "." | "+"

<xalpha> ::= <alpha> | <digit> | <safe> | <extra> | <escape>

<xalphas> ::= <xalpha> | <xalpha> <xalphas>

<xpalpha> ::= <xalpha> | "+"

<xpalphas> ::= <xpalpha> | <xpalpha> <xpalphas>

<ialpha> ::= <alpha> | <alpha> <xalphas>

<alpha> ::= "a" | "b" | "c" | "d" | "e" | "f" | "g" | "h" | "i" | "j" | "k" | "l" | "m" | "n" | "o" | "p" | "q" | "r" | "s" | "t" | "u" | "v" | "w" | "x" | "y" | "z" | "A" | "B" | "C" | "D" | "E" | "F" | "G" | "H" | "I" | "J" | "K" | "L" | "M" | "N" | "O" | "P" | "Q" | "R" | "S" | "T" | "U" | "V" | "W" | "X" | "Y" | "Z"

<digit> ::= "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9"

<safe> ::= "$" | "-" | "_" | "@" | "." | "&" | "+"

<extra> ::= "!" | "*" | "(" | ")" | "," | "'" | '"'

<escape> ::= "%" <hex> <hex>

<hex> ::= <digit> | "a" | "b" | "c" | "d" | "e" | "f" | "A" | "B" | "C" | "D" | "E" | "F"

<digits> ::= <digit> | <digit> <digits>

<alphanum> ::= <alpha> | <digit>

<void> ::= ""
